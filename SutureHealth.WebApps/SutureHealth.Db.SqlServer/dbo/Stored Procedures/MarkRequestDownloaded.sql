CREATE PROCEDURE [dbo].[MarkRequestDownloaded]
	@LegacyRequestIds IntegerKey READONLY,
	@MemberId INT
AS
BEGIN
	DECLARE @TiffInsteadOfPdf BIT = 0;

	IF EXISTS (SELECT 1 FROM [$(SutureSignWeb)].dbo.UserSettings WHERE UserId = @MemberId AND Setting = 'DownloadDocumentAsTiff' AND Active = 1 AND ItemBit = 1)
	BEGIN
		SET @TiffInsteadOfPdf = 1;
	END

	INSERT INTO [$(SutureSignWeb)].dbo.Tasks
	(
		[FormId],
		[UserId],
		[FacilityId],
		[ActionId],
		[TemplateId],
		[PatientId],
		[SubmittedBy],
		[Data],
		[CreateDate],
		[Active],
		[SubmittedByFacility]
	)
	SELECT
		rids.Id,
		uf.UserId,
		uf.FacilityId,
		558,
		r.Template,
		r.Patient,
		uf.UserId,
		-- NOTE: The file name generated by the below statement is primarily done for backwards compatibility purposes with the legacy application.
		--		 The file name may not actually match what is generated by the application as the below function is not used to calculate its value.
		CASE @TiffInsteadOfPdf WHEN 1 THEN 'TIFF' ELSE 'PDF' END +
			' Name: ' + dbo.GetFileNameForRequest(rids.Id, CASE WHEN uf.FacilityId = signer_uf.FacilityId THEN 1 ELSE 0 END) + CASE @TiffInsteadOfPdf WHEN 1 THEN '.tiff' ELSE '.pdf' END +
			', Downloaded on: ' + FORMAT(dbo.GetSutureSignDate(), 'M/d/yyyy hh:mm tt'),
		dbo.GetSutureSignDate(),
		1,
		uf.FacilityId
	FROM @LegacyRequestIds rids
		INNER JOIN [$(SutureSignWeb)].dbo.Requests r ON rids.Id = r.Id
		INNER JOIN [$(SutureSignWeb)].dbo.Users_Facilities submitter_uf ON r.Submitter = submitter_uf.Id
		INNER JOIN [$(SutureSignWeb)].dbo.Users_Facilities signer_uf ON r.Signer = signer_uf.Id
		INNER JOIN [$(SutureSignWeb)].dbo.Users_Facilities uf ON uf.FacilityId IN (submitter_uf.FacilityId, signer_uf.FacilityId) AND uf.UserId = @MemberId
END